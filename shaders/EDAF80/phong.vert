#version 410

// Remember how we enabled vertex attributes in assignmentÂ 2 and attached some
// data to each of them, here we retrieve that data. Attribute 0 pointed to the
// vertices inside the OpenGL buffer object, so if we say that our input
// variable `vertex` is at location 0, which corresponds to attribute 0 of our
// vertex array, vertex will be effectively filled with vertices from our
// buffer.
// Similarly, normal is set to location 1, which corresponds to attribute 1 of
// the vertex array, and therefore will be filled with normals taken out of our
// buffer.
layout (location = 0) in vec3 vertex;
layout (location = 1) in vec3 normal;
layout (location = 2) in vec2 texcoord;

uniform mat4 vertex_model_to_world;
uniform mat4 normal_model_to_world;
uniform mat4 vertex_world_to_clip;
uniform vec3 light_position;
uniform vec3 camera_position;

// This is the custom output of this shader. If you want to retrieve this data
// from another shader further down the pipeline, you need to declare the exact
// same structure as in (for input), with matching name for the structure
// members and matching structure type. Have a look at
// shaders/EDAF80/diffuse.frag.
out VS_OUT {
	vec2 texcoord;
	vec3 fN;
	vec3 fV;
	vec3 fL;
} vs_out;


void main()
{
	vs_out.texcoord = texcoord.xy;

	vec3 world_position = vec3(vertex_model_to_world * vec4(vertex, 1.0));
	vs_out.fN = vec3(normal_model_to_world * vec4(normal, 0.0));
	vs_out.fV = camera_position - world_position;
	vs_out.fL = light_position - world_position;

	gl_Position = vertex_world_to_clip * vertex_model_to_world * vec4(vertex, 1.0);
}



